

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

// This is the library version used when deploying the artifact
group = PROJ_GROUP
version = PROJ_VERSION
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                description PROJ_DESCRIPTION

                // Add your description here
                name PROJ_POM_NAME
                url PROJ_WEBSITEURL

                // Set your license
                licenses {
                    license {
                        name PROJ_LICENCE_NAME
                        url PROJ_LICENCE_URL
                        distribution PROJ_LICENCE_DEST
                    }
                }
                developers {
                    developer {
                        id DEVELOPER_ID
                        name DEVELOPER_NAME
                        email DEVELOPER_EMAIL
                    }
                }
                scm {
                    connection PROJ_VCSURL
                    developerConnection PROJ_VCSURL
                    url PROJ_WEBSITEURL
                }
            }
        }
    }
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            groupId = PROJ_GROUP
            artifactId = PROJ_ARTIFACTID
            version = PROJ_VERSION

            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                asNode().appendNode('name', PROJ_POM_NAME)
                asNode().appendNode('description', PROJ_DESCRIPTION)
                asNode().appendNode('url', PROJ_WEBSITEURL)

                def licenseNode = asNode().appendNode('licenses').appendNode('license')
                licenseNode.appendNode('name', PROJ_LICENCE_NAME)
                licenseNode.appendNode('url', PROJ_LICENCE_URL)
                licenseNode.appendNode('distribution', PROJ_LICENCE_DEST)

                def developerNode = asNode().appendNode('developers').appendNode('developer')
                developerNode.appendNode('id', DEVELOPER_ID)
                developerNode.appendNode('name', DEVELOPER_NAME)
                developerNode.appendNode('email', DEVELOPER_EMAIL)


                def scmNode = asNode().appendNode('scm')
                scmNode.appendNode('connection', PROJ_VCSURL)
                scmNode.appendNode('developerConnection', PROJ_VCSURL)
                scmNode.appendNode('url', PROJ_WEBSITEURL)
            }
        }
    }
}

task sourcesJar(type: Jar) {
    group "artifacts"
    from android.sourceSets.main.java.srcDirs //源码路径
    classifier = 'sources' //文件名后缀
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    group "artifacts"
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    publications = ['MyPublication']
    configurations = ['archives']
    pkg {
        repo = "moonshot.io"
        name = PROJ_NAME
        websiteUrl = PROJ_WEBSITEURL
        vcsUrl = PROJ_VCSURL
        licenses = ["Apache-2.0"]
        publish = true
        version {
            desc = PROJ_DESCRIPTION
            gpg {
                sign = true //Determines whether to GPG sign the files. The default is false
                passphrase = properties.getProperty("bintray.gpg.password")
                //Optional. The passphrase for GPG signing'
            }
        }
    }
}